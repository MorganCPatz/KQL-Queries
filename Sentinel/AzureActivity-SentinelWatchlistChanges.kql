// Query to detect when changes are made to Azure Sentinel Watchlists
// Operations include updates (add/remove item) and deletion of a Watchlist

AzureActivity
| where OperationNameValue startswith "Microsoft.SecurityInsights/watchlists"
| where OperationName has_any ("delete", "write")
| where ActivityStatus == "Succeeded"
| extend URI = tostring(parse_json(HTTPRequest).uri)
| parse URI with * '/workspaces/' Workspace: string '/' *
| parse URI with * '/watchlists/' Delimiter1: string '/' *
| parse URI with * '/watchlists/' Delimiter2: string '?' *
| extend Watchlist = coalesce(Delimiter1, Delimiter2)
| extend ChangedBy = Caller
| project TimeGenerated, OperationName, Watchlist, Workspace, ResourceGroup, ChangedBy
