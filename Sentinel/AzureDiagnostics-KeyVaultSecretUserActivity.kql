// Query to obtain a list of user accounts that Accessed or Updated a KeyVault Secret
// Results do not include Applications; filtered to only check for actions by User Accounts

AzureDiagnostics
| where ResourceProvider == "MICROSOFT.KEYVAULT"
| where OperationName has_any ("SecretGet", "SecretUpdate")
| where isnotempty(identity_claim_upn_s)
| parse requestUri_s with * '.vault.azure.net/secrets/' Delimiter1: string '/' *
| parse requestUri_s with * '.vault.azure.net/secrets/' Delimiter2: string '?' *
| extend Secret = coalesce(tostring(Delimiter1), Delimiter2)
| extend AccountUPN = identity_claim_upn_s
| summarize arg_max(TimeGenerated, *) by Secret, AccountUPN
| project-reorder TimeGenerated, ResourceGroup, Resource, Secret, OperationName, AccountUPN
