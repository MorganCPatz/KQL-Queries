// Query written in a Log Analytics Workspace to Identify permissions assigned to a KeyVault or KeyVault Secret
// ObjectID can represent a user/group/service principal; additional tables can be used to correlate data

let AzureRoleIDs = externaldata(Role: string, ID: string)['https://raw.githubusercontent.com/MorganCPatz/KQL-Queries/refs/heads/main/AzureBuiltinRoleIDs.json'] with (format='multijson');
AzureActivity
| where TimeGenerated > ago (7d)
| where ResourceId contains "Microsoft.KeyVault"
| where OperationName == "Create role assignment"
| extend Scope = parse_json(Authorization).scope 
| parse Scope with * '/secrets/' Secret: string '/' *
| parse Scope with * '/vaults/' KeyVault: string '/' *
| extend ['KeyVault or Secret'] = coalesce(tostring(Secret), KeyVault)
| extend ObjectID = parse_json(tostring(parse_json(tostring(parse_json(Properties).requestbody)).Properties)).PrincipalId
| extend RoleDefinitionID = tostring(parse_json(tostring(parse_json(tostring(parse_json(Properties).requestbody)).Properties)).RoleDefinitionId)
| extend RoleID = extract(@'.*\/(.*)$', 1, RoleDefinitionID)
| lookup AzureRoleIDs on $left.RoleID == $right.ID
| where isnotnull(ObjectID)
| project TimeGenerated, ResourceGroup, KeyVault, ['KeyVault or Secret'], OperationName, Caller, ObjectID, Role
