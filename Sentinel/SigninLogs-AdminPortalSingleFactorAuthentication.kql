// Query to identify accounts that are logging into one of the identified Admin Portals without MFA
// Useful to detect accounts logging in using Single Factor Authentication prior to Microsoft Enforcement
// See https://learn.microsoft.com/en-us/entra/identity/authentication/concept-mandatory-multifactor-authentication?tabs=dotnet for details

// Azure Portal                                 c44b4083-3bb0-49c1-b47d-974e53cbdf3c
// Microsoft Entra Admin Center                 c44b4083-3bb0-49c1-b47d-974e53cbdf3c
// Microsoft Intune Admin Center                c44b4083-3bb0-49c1-b47d-974e53cbdf3c
// Azure Command-line Interfaice (Azure CLI)    04b07795-8ddb-461a-bbee-02f9e1bf7b46
// Azure PowerShell                             1950a258-227b-4e31-a9cf-717495945fc2
// Azure Mobile App                             0c1307d4-29d6-4389-a11c-5cbe7f65d7fa


let Apps = dynamic([
    "c44b4083-3bb0-49c1-b47d-974e53cbdf3c", "04b07795-8ddb-461a-bbee-02f9e1bf7b46",
    "1950a258-227b-4e31-a9cf-717495945fc2", "0c1307d4-29d6-4389-a11c-5cbe7f65d7fa"]);
SigninLogs
| where AppId has_any (Apps)
| where ResultType == "0"
| where AuthenticationRequirement == "singleFactorAuthentication"
| where AADTenantId == ResourceTenantId
| where array_length(ConditionalAccessPolicies) != 0 // Bootstrap
| extend DeviceName = DeviceDetail.displayName
| project TimeGenerated, UserPrincipalName, AppDisplayName, DeviceName
