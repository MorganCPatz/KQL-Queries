// Updated Query to Identify Permissions Added/Remove from a KeyVault or Secret
// ObjectID can represent a user/group/service principal; additional tables can be used to correlate data

let AzureRoleIDs = externaldata(Role: string, ID: string)['https://raw.githubusercontent.com/MorganCPatz/KQL-Queries/refs/heads/main/AzureBuiltinRoleIDs.json'] with (format='multijson');
AzureActivity
| where TimeGenerated > ago (7d)
| where ResourceId contains "Microsoft.KeyVault"
| where OperationName has_any ("Create role assignment", "Delete role assignment")
| parse Authorization with * '/secrets/' Secret: string '/'*
| parse Authorization with * '/vaults/' KeyVault: string '/' *
| extend RequestBodyObjectID = parse_json(tostring(parse_json(tostring(parse_json(Properties).requestbody)).Properties)).PrincipalId
| extend RequestBodyDefinitionID = tostring(parse_json(tostring(parse_json(tostring(parse_json(Properties).requestbody)).Properties)).RoleDefinitionId)
| extend ResponseBodyObjectID = parse_json(tostring(parse_json(tostring(parse_json(Properties).responseBody)).properties)).principalId
| extend ResponseBodyDefinitionID = tostring(parse_json(tostring(parse_json(tostring(parse_json(Properties).responseBody)).properties)).roleDefinitionId)
| extend ['KeyVault or Secret'] = coalesce(tostring(Secret), KeyVault)
| extend ObjectId = coalesce(RequestBodyObjectID, ResponseBodyObjectID)
| extend RoleId = coalesce(extract(@'.*\/(.*)$', 1, RequestBodyDefinitionID), extract(@'.*\/(.*)$', 1, ResponseBodyDefinitionID))
| where isnotempty(ObjectId)
| lookup AzureRoleIDs on $left.RoleId == $right.ID
| project TimeGenerated, OperationName, ResourceGroup, KeyVault, ['KeyVault or Secret'], Caller, ObjectId, Role
