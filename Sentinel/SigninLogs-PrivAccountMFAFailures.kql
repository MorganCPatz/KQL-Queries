// Generates list of Privileged Accounts based on previous PIM Activiations
// Useful to detect MFA failures for privileged accounts

let PIMRoles = dynamic(["Global Administrator", "Privileged Authentication Administrator", "Privileged Role Administrator", "Security Administrator"]);
let AdminAccounts = AuditLogs
   | where TimeGenerated > ago (14d)
   | where OperationName == "Add member to role completed (PIM activation)"
   | where TargetResources[0].displayName has_any (PIMRoles)
   | extend AdminAccount = tostring((InitiatedBy.user).userPrincipalName)
   | distinct tostring(AdminAccount);
SigninLogs
| where TimeGenerated > ago (14d)
| where ResultType != "0"
| where UserPrincipalName in (AdminAccounts)
| mv-expand todynamic(AuthenticationDetails)
| extend MFAResult = tostring((AuthenticationDetails).authenticationStepResultDetail)
| where MFAResult startswith "MFA denied"
| project TimeGenerated, UserPrincipalName, MFAResult, IPAddress
