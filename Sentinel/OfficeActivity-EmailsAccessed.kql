// Query to search emails delivered within X period time and accessed within the past X time
// Example:  Search for emails delivered in the past 30 days and were accessed in the past 12 hours
// Can be used to see a potentially compromised account emails were accessed

let UserMailbox = <User>;
let TimeDelivered = <Time Delivered>;
let MailAccessedTimeframe = <Time Mailbox Accessed>;
let EmailActivity = EmailEvents
    | where TimeGenerated > ago (TimeDelivered)
    | where RecipientEmailAddress contains UserMailbox
    | extend EmailDelivered = TimeGenerated
    | project EmailDelivered, InternetMessageId, SenderFromAddress, Subject;
let newOfficeActivity = OfficeActivity
    | where TimeGenerated > ago (MailAccessedTimeframe)
    | where Operation == "MailItemsAccessed"
    | where MailboxOwnerUPN == UserMailbox
    | mv-expand todynamic(Folders)
    | extend InternetMessageId = tostring(Folders.FolderItems.[0].InternetMessageId)
    | extend TimeAccessed = TimeGenerated
    | project InternetMessageId, UserId, TimeAccessed, Operation
    | join kind=inner(EmailActivity
    ) on InternetMessageId;
newOfficeActivity
    | summarize arg_max(TimeAccessed, *) by Subject
    | project TimeAccessed, UserId, Operation, SenderFromAddress, Subject, EmailDelivered
