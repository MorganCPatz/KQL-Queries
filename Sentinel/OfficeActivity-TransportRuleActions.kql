// Query to detect changes to Exchange Transport Rules
// Displays summary of changes to transport rule

let TransportRules = dynamic(["Disable-TransportRule", "Enable-TransportRule", "New-TransportRule", "Set-TransportRule"]);
OfficeActivity
| where TimeGenerated > ago (1d)
| where Operation has_any (TransportRules)
| mv-expand todynamic(Parameters)
| extend ParameterName = Parameters.Name,
         ParameterValue = Parameters.Value
| project TimeGenerated,
          Operation,
          ['Transport Rule'] = OfficeObjectId,
          ['Changed By'] = UserId,
          ParameterName,
          ParameterValue
