// Useful to search for login failures for admin or privileged accounts based on upn
// ErrorCode lookup maybe necessary if FailureReason is listed as "General Failure"

let ResolvedErrorCodes = externaldata(Error: string, Description: string)['https://raw.githubusercontent.com/MorganCPatz/PSFunctions/main/EntraIDErrorCodes.json'] with (format='multijson');
IdentityLogonEvents
    | where Application != "Active Directory"
    | where ActionType == "LogonFailed"
    | where AccountUpn contains "<Account Naming Convention"
    | extend IdentityLogonRequestId = tostring(AdditionalFields["Request ID"])
    | project Timestamp,IdentityLogonRequestId, AccountUpn, FailureReason
| join (AADSignInEventsBeta
    | where LogonType == '["interactiveUser"]'
    | extend ErrorCode = tostring(ErrorCode)
    | project Timestamp, AccountDisplayName, DeviceName, IPAddress, RequestId, ErrorCode
    | lookup ResolvedErrorCodes on $left.ErrorCode == $right.Error
) on $left.IdentityLogonRequestId == $right.RequestId
| project Timestamp, AccountDisplayName, AccountUpn, DeviceName, IPAddress, ErrorCode, FailureReason, Description
