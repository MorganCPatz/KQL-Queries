// Returns a list of accounts added to the the builtin\Local groups on device
// Excludes accounts added by local system

DeviceEvents
| where Timestamp > ago (30d)
| where ActionType == "UserAccountAddedToLocalGroup"
| where InitiatingProcessAccountSid !="S-1-5-18"
| extend AddtlFields = parse_json(AdditionalFields)
| project Timestamp, DeviceName, InitiatingProcessAccountName, AccountSid, AddtlFields
| join kind=inner IdentityInfo on $left.AccountSid==$right.OnPremSid
| summarize arg_max(Timestamp, *) by DeviceName, AccountUpn
| project Timestamp,
          DeviceName,
          ["Modified Group"] = AddtlFields["GroupName"],
          ["Account Added"] = AccountUpn,
          ["Account Added By"] = InitiatingProcessAccountName
| sort by Timestamp desc
