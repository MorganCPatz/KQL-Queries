// Used to detect non local\system initiated changes to the PowerShell Execution Policy
// Modify 'ExecutionSettings' to add/remove additional settings

let ExecutionSettings = dynamic(["Bypass", "Unrestricted"]);
DeviceEvents
| where ActionType == "PowerShellCommand"
| where InitiatingProcessAccountName != "system"
| extend AdditionalDetails = parse_json(AdditionalFields)
| where AdditionalDetails.['Command'] == "Set-ExecutionPolicy" and InitiatingProcessCommandLine has_any (ExecutionSettings)
| extend ChangedBy = strcat(toupper(InitiatingProcessAccountDomain), "\\", InitiatingProcessAccountName)
| project Timestamp, DeviceName, ChangedBy, InitiatingProcessCommandLine
| summarize arg_max(Timestamp, InitiatingProcessCommandLine) by DeviceName, ChangedBy
| project-reorder Timestamp
| sort by Timestamp desc
